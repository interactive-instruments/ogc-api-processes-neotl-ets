import '../../../shared/Assertions.neotl'
import "../4_Processes/Processes-tcs.neotl"
import "Actions.neotl"


TestCase "Check for an echo process" {
    id: jobs.echo
    description: "Check that an echo process with the id 'echo' or 'EchoProcess' exists"
    references:
    - "Processes /processes"
        "https://docs.ogc.org/DRAFTS/18-062.html#_jobs"
        AbstractTestCase

    ValidationStep "Extract EchoProcess ID" {
        id: extract
        description: "Check that an echo process with the id 'echo' or 'EchoProcess' exists"

        given:
            - Response from process.list.encoding.json

        when: Extractor extractors.echo.ids executed

        then:
            - Assertion atLeastOneValueExtracted must pass
    }
}

TestCase "Request the process description of the the echo process" {
    id: jobs.echo.description
    description: "Check that the echo process description can be retrieved"
    references:
    - "Processes /processes"
        "https://docs.ogc.org/DRAFTS/18-062.html#_jobs"
        AbstractTestCase

    ValidationStep "Request EchoProcess description" {
        id: request
        description: "Check that the echo process description can be retrieved"

        given:
            - Value ${echoId} from jobs.echo.extract

        when: Request requestGetProcessDescription executed

        then:
            - Assert OpenAPI3 {
                schema 
                "${schemaUrl}/process.yaml"
                validates
            }
            - AssertionGroup assertions.json.success must pass
    }
}

TestCase "Execute the echo process" {
    // /conf/core/job-creation-request
    id: jobs.echo.execute
    description: "Execute the echo process and validate the reponse"
    references:
    - "Processes /processes"
        "https://docs.ogc.org/DRAFTS/18-062.html#_jobs"
        AbstractTestCase

    ValidationStep "Execute echo process respond-sync" {
        id: coreJobs3step1
        description: "Execute the echo process with the extracted echoId"

        given:
            - Value ${echoId} from jobs.echo.extract

        when: 
            Request executeEcho executed with:
                - ${preferHeader} = "respond-sync"

        then:
            - Assert OpenAPI3 {
                schema 
                "${schemaUrl}/output.yaml"
                validates
            }
            - AssertionGroup assertions.json.success must pass
    }
}


Assertion "At least one value exists" {
    id: atLeastOneValueExtracted

    JSON {
        ${echoId} exists
        otherwise FAIL with "No echo ID could be extracted"
    }
}